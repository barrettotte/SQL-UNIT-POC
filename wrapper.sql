-- SQL-Unit wrapper for executing SQL string within a T-SQL transaction --

CREATE OR ALTER PROCEDURE [dbo].[SQLUnit_Wrapper](
	@SQL_STRING NVARCHAR(MAX),
	@RES_COLS NVARCHAR(3000)
)  
WITH EXECUTE AS CALLER 
AS
BEGIN
	SET NOCOUNT ON

	DECLARE @POS INT = 0;
	DECLARE @LEN INT = 0;
	DECLARE @VAL VARCHAR(255);
	DECLARE @DELIM_COL VARCHAR(3) = '|,|';

	-- Dynamically create temp table from expected columns --
	IF OBJECT_ID('tempdb..#TMP') IS NOT NULL 
		DROP TABLE #TMP;
	CREATE TABLE #TMP(__IGNORE__ INT);

	WHILE CHARINDEX(@DELIM_COL, @RES_COLS, @POS + 1) > 0 
		BEGIN
			SET @LEN = CHARINDEX(@DELIM_COL, @RES_COLS, @POS + 1) - @POS;
			SET @VAL = SUBSTRING(@RES_COLS, @POS, @LEN);
			EXEC('ALTER TABLE #TMP ADD ' + @VAL);
			SET @POS = CHARINDEX(@DELIM_COL, @RES_COLS, @POS + @LEN) + LEN(@DELIM_COL);
		END
	EXEC('ALTER TABLE #TMP DROP COLUMN __IGNORE__');

	-- Execute injected SQL and select result set --
	SAVE TRANSACTION SQLUnit_Wrapper
		BEGIN TRY
			SET @SQL_STRING = REPLACE(@SQL_STRING, '&!SQ!&', CHAR(39));
			SET @SQL_STRING = REPLACE(@SQL_STRING, '&!NL!&', CHAR(13) + CHAR(10));
			EXEC('CREATE OR ALTER PROCEDURE [dbo].[SQLUnit_TmpSP] AS ' + @SQL_STRING);
			INSERT INTO #TMP EXEC [dbo].[SQLUnit_TmpSP];
			SELECT * FROM #TMP;
			EXEC('DROP PROCEDURE [dbo].[SQLUnit_TmpSP]');
		END TRY
		BEGIN CATCH
			DECLARE @EXEC_FAILED BIT = 1;
			SELECT
				@EXEC_FAILED AS __SQLUnit_FAILED__,
				@SQL_STRING AS SQL_STRING,
				@RES_COLS AS RES_COLS,
				ERROR_NUMBER() AS ErrorNumber,
				ERROR_SEVERITY() AS ErrorSeverity,
				ERROR_STATE() AS ErrorState,
				ERROR_PROCEDURE() AS ErrorProcedure,
				ERROR_LINE() AS ErrorLine,
				ERROR_MESSAGE() AS ErrorMessage;
			IF @@TRANCOUNT > 0
				ROLLBACK TRANSACTION SQLUnit_Wrapper
		END CATCH
	ROLLBACK TRANSACTION SQLUnit_Wrapper
	DROP TABLE #TMP;
END

